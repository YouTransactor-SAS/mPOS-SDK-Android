apply plugin: 'com.android.application'

android {
    signingConfigs {
        release
    }

    compileSdkVersion 33

    defaultConfig {
        applicationId "com.youtransactor.sampleapp"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 3449
        versionName "3.4.49"
        multiDexEnabled true
    }

    buildTypes {
        //dev
        debug {
            debuggable true
            versionNameSuffix ".debug"
        }

        //test
        internal {
            initWith debug
            versionNameSuffix ".Test"
            signingConfig signingConfigs.release
        }

        //prod
        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

def Properties props = new Properties()
def propFile = rootProject.file('keystore.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE')
            && props.containsKey('STORE_PASSWORD')
            && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}


dependencies {

    /* uCube lib dependencies */
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'com.google.guava:guava:30.0-jre'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.github.tony19:logback-android:1.3.0-2'

    /* App dependencies */
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.5.0' //use this when android version > 4.4
    //implementation 'com.android.support:design:30.0.0' //use this when android version <= 4.4
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'


    implementation files('libs/libApp-3.4.49.aar')
}
