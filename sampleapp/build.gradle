/*
 * ============================================================================
 *
 * Copyright (c) 2022 YouTransactor
 *
 * All Rights Reserved.
 *
 * This software is the confidential and proprietary information of YouTransactor
 * ("Confidential Information"). You  shall not disclose or redistribute such
 * Confidential Information and shall use it only in accordance with the terms of
 * the license agreement you entered into with YouTransactor.
 *
 * This software is provided by YouTransactor AS IS, and YouTransactor
 * makes no representations or warranties about the suitability of the software,
 * either express or implied, including but not limited to the implied warranties
 * of merchantability, fitness for a particular purpose or non-infringement.
 * YouTransactor shall not be liable for any direct, indirect, incidental,
 * special, exemplary, or consequential damages suffered by licensee as the
 * result of using, modifying or distributing this software or its derivatives.
 *
 * ==========================================================================
 */

plugins {
    id 'com.android.application'
}

android {
    namespace "com.youtransactor.sampleapp"

    signingConfigs {
        release
    }

    compileSdkVersion 33
    defaultConfig {
        applicationId "com.youtransactor.sampleapp"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 3453
        versionName "3.4.53"
        multiDexEnabled true
    }

    buildTypes {
        //dev
        debug {
            debuggable true
            versionNameSuffix ".debug"
        }

        //test
        internal {
            initWith debug
            versionNameSuffix ".Test"
            signingConfig signingConfigs.release
        }

        //prod
        release {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

Properties props = new Properties()
def propFile = rootProject.file('local.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('KEYSTORE_FILE')
            && props.containsKey('KEYSTORE_PASSWORD')
            && props.containsKey('KEY_ALIAS')
            && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['KEYSTORE_FILE'])
        android.signingConfigs.release.storePassword = props['KEYSTORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'local.properties found but some keystore entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'local.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    /* uCube lib dependencies */
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.google.guava:guava:30.0-jre'
    implementation files('libs/JPS_libApp_3.4.53.aar')

    implementation 'com.google.android.material:material:1.5.0' //use this when android version > 4.4
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    /* used for locale update demo */
    implementation files('libs/android-file-chooser-v1.2.0-final.aar')

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:3.0-alpha-1'
}